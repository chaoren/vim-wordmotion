Execute (Check default mappings):
  for map in [ 'iw', 'aw', 'iW', 'aW' ]
    for mode in [ 'x', 'o' ]
      AssertEqual '<Plug>WordMotion_' . map, mapcheck(map, mode)
      Assert mapcheck('<Plug>WordMotion_' . map, mode) =~ '_AOrInnerWordMotion('
      execute mode . 'unmap' map
      Assert !hasmapto('<Plug>WordMotion_' . map, mode)
      execute mode . 'unmap' '<Plug>WordMotion_' . map
    endfor
  endfor
  Assert !hasmapto('_AOrInnerWordMotion(', 'nvxsoilc')
  for map in [ 'w', 'e', 'b', 'ge', 'W', 'E', 'B', 'gE' ]
    for mode in [ 'n', 'x', 'o' ]
      AssertEqual '<Plug>WordMotion_' . map, mapcheck(map, mode)
      Assert mapcheck('<Plug>WordMotion_' . map, mode) =~ '_WordMotion('
      execute mode . 'unmap' map
      Assert !hasmapto('<Plug>WordMotion_' . map, mode)
      execute mode . 'unmap' '<Plug>WordMotion_' . map
    endfor
  endfor
  Assert !hasmapto('_WordMotion(', 'nvxsoilc')
  for map in [ '<C-R><C-W>', '<C-R><C-A>' ]
    AssertEqual '<Plug>WordMotion_' . map, mapcheck(map, 'c')
    Assert mapcheck('<Plug>WordMotion_'  . map, 'c') =~ '_GetCurrentWord('
    execute 'cunmap' map
    Assert !hasmapto('<Plug>WordMotion_' . map, 'c')
    execute 'cunmap' '<Plug>WordMotion_' . map
  endfor
  Assert !hasmapto('_GetCurrentWord(', 'nvxsoilc')
  Assert !hasmapto('<Plug>WordMotion_', 'nvxsoilc')

Execute (Check disable default mappings):
  let g:wordmotion_disable_default_mappings = 1
  unlet! g:wordmotion_prefix
  unlet! g:wordmotion_mappings
  unlet! g:loaded_wordmotion
  runtime plugin/wordmotion.vim
  for map in [ 'iw', 'aw', 'iW', 'aW' ]
    for mode in [ 'x', 'o' ]
      Assert empty(mapcheck(map, mode))
      Assert mapcheck('<Plug>WordMotion_' . map, mode) =~ '_AOrInnerWordMotion('
    endfor
  endfor
  for map in [ 'w', 'e', 'b', 'ge', 'W', 'E', 'B', 'gE' ]
    for mode in [ 'n', 'x', 'o' ]
      Assert empty(mapcheck(map, mode))
      Assert mapcheck('<Plug>WordMotion_' . map, mode) =~ '_WordMotion('
    endfor
  endfor
  for map in [ '<C-R><C-W>', '<C-R><C-A>' ]
    Assert empty(mapcheck(map, 'c'))
    Assert mapcheck('<Plug>WordMotion_'  . map, 'c') =~ '_GetCurrentWord('
  endfor
  Assert !hasmapto('<Plug>WordMotion_', 'nvxsoilc')

Execute (Reset plugin for further tests):
  unlet! g:wordmotion_disable_default_mappings
  unlet! g:loaded_wordmotion
  runtime plugin/wordmotion.vim
